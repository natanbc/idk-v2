plugins {
    id 'java-library'
    id 'maven'
    id 'maven-publish'
    id 'signing'
}

def versionObj = new Version(major: 0, minor: 4, revision: 0)

dependencies {
    api project(':idk-ast')
    api project(':idk-common')
    api project(':idk-compiler')
    api project(':idk-interpreter')
    api project(':idk-ir')
    api project(':idk-parser')
    api project(':idk-runtime')
    api project(':idk-stdlib')
}

def lint = [
        "auxiliaryclass",
        "cast",
        "deprecation",
        "dep-ann",
        "divzero",
        "empty",
        "exports",
        "fallthrough",
        "finally",
        "-module",
        "opens",
        "options",
        "overloads",
        "overrides",
        "path",
        "rawtypes",
        "removal",
        "-requires-transitive-automatic",
        "static",
        "try",
        "unchecked",
        "varargs",
        "preview"
]

allprojects {
    group 'com.github.natanbc.idk'
    version versionObj.toString()

    sourceCompatibility = 10
    targetCompatibility = 10
    
    repositories {
        jcenter()
        mavenCentral()
    }

    compileJava {
        options.encoding = 'UTF-8'
        options.incremental = true
        options.compilerArgs += ["-Xlint:${lint.join(",")}", "-Werror"]
    }

    //gradle is retarded
    //"Required org.gradle.jvm.version '10' and found incompatible value '12'."
    java {
        disableAutoTargetJvm()
    }
}

def publishTargets = []

subprojects {
    apply plugin: 'maven-publish'
    
    publishTargets += name
    afterEvaluate {
        javadoc {
            options.addStringOption('-module-path', classpath.asPath)
        }

        compileJava {
            inputs.property("moduleName", name)
            doFirst {
                options.compilerArgs += [
                        '--module-path', classpath.asPath,
                ]
                classpath = files()
            }
        }
    }

    task sourceJar(type: Jar) {
        classifier "sources"
        from sourceSets.main.allJava
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier "javadoc"
        from javadoc.destinationDir
    }

    afterEvaluate {
        publishing {
            publications {
                "$name"(MavenPublication) {
                    from components.java
                    groupId group
                    artifactId name

                    artifact sourceJar
                    artifact javadocJar
                }
            }
            repositories {
                maven { url "$buildDir/repo" }
            }
        }
        signing {
            sign publishing.publications."$name"
        }
    }
}

class Version {
    String major, minor, revision

    String toString() {
        if(revision == null || revision == "0") {
            return "$major.$minor"
        } else {
            return "$major.$minor.$revision"
        }
    }
}
